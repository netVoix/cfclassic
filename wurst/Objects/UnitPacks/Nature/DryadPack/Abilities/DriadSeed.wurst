package DriadSeed

import BuffObjEditing
import Icons
import ClosureEvents
import ClosureTimers
import ChannelAbilityPreset
import AttachmentPoints

public let SEED_ID = compiletime(ABIL_ID_GEN.next())

public let BUFF_DURATION = 7.01
public let BASE_DAMAGE = 75.
public let PREIODICAL_DAMAGE = 55.

public let TOOLTIP1 = "Seed"
public let TOOLTIP2 = "Seed sprouts inside unit, taking life from it"
public let SEED_EFFECT_PATH = "Windwalk Necro Soul.mdx"
public let SEED_BUFF_OBJ = compiletime(createDummyBuffObject(TOOLTIP1, TOOLTIP2, Icons.pASBTNMagicImmunity, SEED_EFFECT_PATH, AttachmentPoints.origin))

let seedBuffId = SEED_BUFF_OBJ.abilId

@compiletime function createSeed()
    new AbilityDefinitionParasite(SEED_ID)
    ..presetBuffs(lvl -> "B00A")
    ..presetCooldown(lvl -> 16)
    ..presetManaCost(lvl -> 70)
    ..presetDurationNormal(lvl -> 0.01)
    ..presetDurationHero(lvl -> 0.01)
    ..setMissileArt("")
    ..presetCastRange(lvl -> 600)
    ..presetTargetsAllowed(lvl -> "{0},{1},{2},{3}".format(
      TargetsAllowed.air, TargetsAllowed.ground, TargetsAllowed.enemies, TargetsAllowed.nonhero
    ))
    ..setRequirements("")


init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST) ->
        let t = GetSpellTargetUnit()
        let sid = GetSpellAbilityId()
        let u = GetTriggerUnit()
        if sid == SEED_ID and t.isAlive()
            t.addEffect("Holy Light Nature.mdx", AttachmentPoints.origin)
            u.damageTarget(t, BASE_DAMAGE)
            
            if GetSpellAbilityId() == SEED_ID
                doAfter(0.25) -> 
                    t.addAbility(seedBuffId)

            doAfter(BUFF_DURATION) ->
                t.removeAbility(seedBuffId)
            
            doPeriodically(1) cb1 ->
                if t.hasAbility(seedBuffId) and t.isAlive()
                    t.addEffect("Objects\\Spawnmodels\\NightElf\\EntBirthTarget\\EntBirthTarget.mdl", AttachmentPoints.origin)
                    u.damageTarget(t, PREIODICAL_DAMAGE, false, false, ATTACK_TYPE_MAGIC, DAMAGE_TYPE_MAGIC, null)
                else
                    destroy cb1
