package DriadSeed

import BuffObjEditing
import Icons
import ClosureEvents
import ClosureTimers
import HashMap
import ChannelAbilityPreset
import AttachmentPoints

public let SEED_ID = compiletime(ABIL_ID_GEN.next())

public let TOOLTIP1 = "Seed"
public let TOOLTIP2 = "The seed sprouts in the unit, taking life from it"
public let SEED_EFFECT_PATH = "Windwalk Necro Soul.mdx"

public let SEED_BUFF_OBJ = compiletime(createDummyBuffObject(TOOLTIP1, TOOLTIP2, Icons.pASBTNMagicImmunity, SEED_EFFECT_PATH, AttachmentPoints.origin))

let seedBuffId = SEED_BUFF_OBJ.abilId
let seedBuffMap = new HashMap<unit, CallbackSingle>()

@configurable public let BUFF_DURATION = 6.5
@configurable public let BASE_DAMAGE = 75.
@configurable public let PREIODICAL_DAMAGE = 30.

@compiletime function createSeed()
    new AbilityDefinitionParasite(SEED_ID)
    ..presetBuffs(lvl -> "B00A")
    ..presetCooldown(lvl -> 12)
    ..presetManaCost(lvl -> 75)
    ..presetDurationNormal(lvl -> 0.01)
    ..presetDurationHero(lvl -> 0.01)
    ..setMissileArt("")
    ..presetCastRange(lvl -> 600)
    ..presetTargetsAllowed(lvl -> "{0},{1},{2},{3}".format(
      TargetsAllowed.air, TargetsAllowed.ground, TargetsAllowed.enemies, TargetsAllowed.nonhero
    ))
    ..setRequirements("")

init
    EventListener.onTargetCast(SEED_ID) (caster, tpos) ->
        let t = GetSpellTargetUnit()
        AddSpecialEffectTarget("Holy Light Nature.mdx", tpos, AttachmentPoints.origin)
        caster.damageTarget(t, BASE_DAMAGE)
        
        if GetSpellAbilityId() == SEED_ID
            doAfter(0.25) -> 
                t.addAbility(seedBuffId)

        if seedBuffMap.has(t)
            destroy seedBuffMap.get(t)

        let cb = doAfter(BUFF_DURATION) ->
            if seedBuffMap.has(t)
                seedBuffMap.remove(t)
                t.removeAbility(seedBuffId)
        seedBuffMap.put(t, cb)
        
        doPeriodically(0.5) cb1 ->
            if t.hasAbility(seedBuffId)
                caster.damageTarget(t, PREIODICAL_DAMAGE, false, false, ATTACK_TYPE_MAGIC, DAMAGE_TYPE_MAGIC, null)
            else
                destroy cb1
