package ResqueStrike

import ResqueStrikeObjects
import Orders
import ClosureTimers
import UnitHelpers

public tuple resqueStrikeData(int killed, real damage)

public class ResqueStrike
  private var data = resqueStrikeData(0, 0)
  private unit caster
  private vec2 pos


  construct(unit unitCaster, vec2 targetPos)
    caster = unitCaster
    pos = targetPos

  function cast()
    let owner = caster.getOwner()
    removeAbility(caster)
    makeLocationEffect(owner, pos)
    makeCasterEffect(caster)
    saveOwner(owner)
    doAfter(0.35) ->
      let newData = makeAction(caster, pos)
      updateData(newData)

  function getData() returns resqueStrikeData
    return data

  private function updateData(resqueStrikeData newData)
    data.damage = data.damage + newData.damage
    data.killed = data.killed + newData.killed

  private function removeAbility(unit caster)
    caster..removeAbility(RESQUE_STRIKE_ID)
          ..removeAbility(RESQUE_STRIKE_CASTER_EFFECT_ID)

  private function makeCasterEffect(unit caster)
    addEffect("Abilities\\Spells\\Items\\TomeOfRetraining\\TomeOfRetrainingCaster.mdl", caster, "origin").destr()

  private function makeLocationEffect(player owner, vec2 pos)
    let effectUnit = createUnit(owner, RESQUE_STRIKE_EFFECT_DUMMY_ID, pos, angle(0))
      ..setVertexColor(colorA(0, 0, 0, 0))
      ..addEffect("Units\\NightElf\\Wisp\\WispExplode.mdl", "origin")
      ..addEffect("Units\\NightElf\\Wisp\\WispExplode.mdl", "origin")
      ..addEffect("Units\\NightElf\\Wisp\\WispExplode.mdl", "origin")
      ..issueImmediateOrderById(OrderIds.fanofknives)
    doAfter(1.5) () ->
      effectUnit.remove()

  private function makeAction(unit caster, vec2 pos) returns resqueStrikeData
    let owner = caster.getOwner()
    var killed = 0
    var damage = 0.
    for u from CreateGroup()..enumUnitsInRange(pos, 700)
      if u.isEnemyOf(owner) and u.isAliveTrick() and u.isVulnerable()
        damage = damage + u.getLife()
        caster.damageTarget(u, 4444., true, false, ATTACK_TYPE_CHAOS, DAMAGE_TYPE_DEATH, WEAPON_TYPE_WHOKNOWS)
        caster.damageTarget(u, 4444., true, false, ATTACK_TYPE_MAGIC, DAMAGE_TYPE_DEATH, WEAPON_TYPE_WHOKNOWS)
        killed++
    return resqueStrikeData(killed, damage)

  private function saveOwner(player owner)
    g4 = owner
