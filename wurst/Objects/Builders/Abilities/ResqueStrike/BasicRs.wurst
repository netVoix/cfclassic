package BasicRs

import ChannelAbilityPreset
import ResqueStrike
import ClosureTimers
import Orders
import UnitHelpers
import UnitIds

public let RESQUE_STRIKE_ID = 'A005'
public let RESQUE_STRIKE_CASTER_EFFECT_ID = 'A06E'
public let RESQUE_STRIKE_EFFECT_DUMMY_ID = 'h04X'

public let basicRs = new ResqueStrike((owner, pos) -> basicRsLocationEffect(owner, pos),
                                    (caster) ->  basicRsCasterEffect(caster),
                                    (caster, pos) -> basicRsAction(caster, pos))

function basicRsCasterEffect(unit caster)
    caster.addEffect("Abilities\\Spells\\Items\\TomeOfRetraining\\TomeOfRetrainingCaster.mdl", "origin").destr()
                                    
function basicRsLocationEffect(player owner, vec2 pos)
    let effectUnit = createUnit(owner, RESQUE_STRIKE_EFFECT_DUMMY_ID, pos, angle(0))
        ..setVertexColor(colorA(0, 0, 0, 0))
        ..addEffect("Units\\NightElf\\Wisp\\WispExplode.mdl", "origin")
        ..addEffect("Units\\NightElf\\Wisp\\WispExplode.mdl", "origin")
        ..addEffect("Units\\NightElf\\Wisp\\WispExplode.mdl", "origin")
        ..issueImmediateOrderById(OrderIds.fanofknives)
    doAfter(1.5) () ->
        effectUnit.remove()
                                    
function basicRsAction(unit caster, vec2 pos) returns resqueStrikeData
    let owner = caster.getOwner()
    var killed = 0
    var damage = 0.
    for u from CreateGroup()..enumUnitsInRange(pos, 700)
        if u.isEnemyOf(owner) and u.isAliveTrick() and u.isVulnerable()
            damage = damage + u.getLife()
            caster.damageTarget(u, 4444., true, false, ATTACK_TYPE_CHAOS, DAMAGE_TYPE_DEATH, WEAPON_TYPE_WHOKNOWS)
            caster.damageTarget(u, 4444., true, false, ATTACK_TYPE_MAGIC, DAMAGE_TYPE_DEATH, WEAPON_TYPE_WHOKNOWS)
            killed++
    return resqueStrikeData(killed, damage)


@compiletime function resqueStrike()
    new ChannelAbilityPreset(RESQUE_STRIKE_ID, 1, true)
    ..presetAreaofEffect(lvl -> 700)
    ..presetIcon("ReplaceableTextures\\CommandButtons\\BTNHolyBolt.blp")
    ..setButtonPositionNormalX(3)
    ..presetCastingTime(lvl -> 0)
    ..presetCooldown(lvl -> 60)
    ..presetManaCost(lvl -> 0)
    ..presetArtDuration(lvl -> 0)
    ..presetTargetTypes(Targettype.POINT)
    ..presetOption(Option.TARGETIMAGE, true)
    ..presetDurationNormal(lvl -> 1)
    ..presetDurationHero(lvl -> 1)
    ..setArtEffect("")
    ..presetHotkey("V")
    ..setName("Rescue Strike")
    ..presetCastRange(lvl -> 1000)
    ..setArtSpecial("")
    ..presetTargetsAllowed(lvl -> "air,enemies,ground,sapper")
    ..presetTooltipNormal(lvl -> "Rescue Strike [|cffffcc00V|r]")
    ..presetTooltipNormalExtended(lvl -> "Kills all enemies in the target area and reveals stealthed enemies in a larger area.|n|n"
                                        + "|cffFF0000WARNING: You can use this spell only once per round. Use it wisely, it decides most matches!|r")


@compiletime function resqueStrikeEffect()
    new AbilityDefinition(RESQUE_STRIKE_CASTER_EFFECT_ID, 'Asp1')
    ..setMissileArt("Units\\Human\\HeroBloodElf\\BloodElfBall.mdl")
    ..setMissileSpeed(900)
    ..setName("Rescue Strike Effect")
    ..setArtTarget("Abilities\\Spells\\Items\\AIfb\\AIfbTarget.mdl")
    ..setTargetAttachmentPoint("hand,right")
    ..setTargetAttachmentPoint1("hand,left")
    ..setTargetAttachments(2)

@compiletime function resqueStrikeEffectUnit()
    new UnitDefinition(RESQUE_STRIKE_EFFECT_DUMMY_ID, UnitIds.priest)
    ..setNormalAbilities("Aloc,Avul")
    ..setIconGameInterface("ReplaceableTextures\\WorldEditUI\\Editor-Random-Unit.blp")
    ..setDefaultActiveAbility("")
    ..setAnimationCastBackswing(0.01)
    ..setAnimationCastPoint(1.0)
    ..setDeathTimeseconds(0.1)
    ..setDeathType(1)
    ..setModelFile("units\\human\\HeroMountainKing\\HeroMountainKing")
    ..setFoodCost(0)
    ..setHitPointsMaximumBase(4444)
    ..setProjectileLaunchZ(150.0)
    ..setLevel(1)
    ..setScalingValue(3.0)
    ..setMovementHeight(10.0)
    ..setMovementType(MovementType.Fly)
    ..setName("RescueDummy")
    ..setPointValue(0)
    ..setUnitClassification("tauren,ward")
    ..setShadowImageUnit("")
    ..setUnitSoundSet("")
    ..setUpgradesUsed("")
    ..setAttacksEnabled(0)